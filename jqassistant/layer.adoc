[[layer:Default]]
[role=group,includesConstraints="layer:LayerDependencyViolation",includesConcepts="layer:LayerDependencyDiagram,layer:LayerDependency.graphml"]
== Layer

This chapter describes the concepts and constraints related to the layered package structure.

=== Concepts

The root package of the application is "org.springframework.samples.petclinic".

[[layer:Root]]
.Labels the root package of the main artifact as `Root`
[source,cypher,role=concept,requiresConcepts="maven:MainArtifact"]
----
MATCH
  (:Main:Artifact)-[:CONTAINS]->(rootPackage:Package)
WHERE
  rootPackage.fqn = "org.springframework.samples.petclinic"
SET
  rootPackage:Root
RETURN
  rootPackage as RootPackage
----

The application has a layered structure. Each layer is represented by a package located directly in the root package:

- web
- service
- repository
- model

[[layer:Layer]]
.Labels all direct sub-packages of the root package as `Layer`.
[source,cypher,role=concept,requiresConcepts="layer:Root"]
----
MATCH
  (:Root:Package)-[:CONTAINS]->(layer:Package)
WHERE
  layer.name in [
    "web",
    "service",
    "repository",
    "model"
  ]
SET
  layer:Layer
RETURN
  layer as Layer
----

A dependency between two layers exists if there are dependencies between Java types in any of the sub-packages.

[[layer:LayerDependency]]
[source,cypher,role=concept,requiresConcepts="layer:Layer"]
.Adds a relation `DEPENDS_ON` between two layers if there are type dependencies between them.
----
MATCH
  (layer1:Layer:Package)-[:CONTAINS*]->(type1:Type),
  (layer2:Layer:Package)-[:CONTAINS*]->(type2:Type),
  (type1)-[d:DEPENDS_ON]->(type2)
WHERE
  layer1 <> layer2
WITH
  layer1, layer2, count(d) as weight
MERGE
  (layer1)-[d:DEPENDS_ON]->(layer2)
SET
  d.weight = weight
RETURN
  layer1 as Dependent, layer2 as Dependency, weight as Weight
----


There are definitions of allowed dependencies between layers.

[[layer:LayerDependencyDefinition]]
[source,cypher,role=concept,requiresConcepts="layer:Layer",reportType="graphml"]
.Adds `DEFINES_DEPENDENCY` relations between layers for allowed dependencies.
----
MATCH
  (web:Layer {name:"web"}),
  (service:Layer {name:"service"}),
  (repository:Layer {name:"repository"}),
  (model:Layer {name:"model"})
CREATE UNIQUE
 (web)-[w1:DEFINES_DEPENDENCY]->(service),
 (web)-[w2:DEFINES_DEPENDENCY]->(model),
 (service)-[s1:DEFINES_DEPENDENCY]->(repository),
 (service)-[s2:DEFINES_DEPENDENCY]->(model),
 (repository)-[r1:DEFINES_DEPENDENCY]->(model)
RETURN
  *
----

link:graphml/layer_LayerDependencyDefinition.graphml[GraphML Report]

=== Constraints

[[layer:LayerDependencyViolation]]
[source,cypher,role=constraint,requiresConcepts="layer:LayerDependency,layer:LayerDependencyDefinition",primaryReportColumn="Type"]
.There must be no dependencies between layers that are not explicitly defined.
----
MATCH
  (layer1:Layer)-[:DEPENDS_ON]->(layer2:Layer)
WHERE NOT
  (layer1)-[:DEFINES_DEPENDENCY]->(layer2)
WITH
  layer1, layer2
MATCH
  (layer1)-[:CONTAINS*]->(type1:Type),
  (layer2)-[:CONTAINS*]->(type2:Type),
  (type1)-[:DEPENDS_ON]->(type2)
RETURN
  layer1.name as Layer, type1 as Type, layer2.name as LayerDependency, type2 as TypeDependency
----

=== Reports

[[layer:LayerDependencyDiagram]]
[source,cypher,role=concept,requiresConcepts="layer:LayerDependency",reportProperties="render=component-diagram"]
.Creates a diagram about dependencies between layers.
----
MATCH
  (layer:Layer)
OPTIONAL MATCH
  (layer)-[dependsOn:DEPENDS_ON]->(:Layer)
RETURN
  *
----

[[layer:LayerDependency.graphml]]
[source,cypher,role=concept,requiresConcepts="layer:LayerDependency",reportType="graphml"]
.Creates a GraphML report about dependencies between layers and their contained types.
----
MATCH
  (layer:Layer:Package)-[:CONTAINS*]->(type:Type)
OPTIONAL MATCH
  (type)-[dependsOn:DEPENDS_ON]->(:Type)
RETURN {
  role : "graph",
  parent : layer,
  nodes : collect(type),
  relationships : collect(dependsOn)
} as TypesPerLayer
----

link:graphml/layer_LayerDependency.graphml[GraphML Report]
