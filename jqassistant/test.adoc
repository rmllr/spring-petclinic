[[test:Default]]
[role=group,includesConstraints="test:TestMethodWithoutAssertion,junit4:IgnoreWithoutMessage",includesConcepts="test:TestAffectedByLastCommit"]
== Tests

The following test rules apply:

- <<test:TestMethodWithoutAssertion>>
- All @Ignore annotations must provide a message

The following reports are created:

- <<test:TestAffectedByLastCommit>>

=== Constraints

[[test:TestMethodWithoutAssertion]]
[source,cypher,role=constraint,requiresConcepts="junit4:TestMethod,assertj:AssertMethod,spring-test-web:Assert"]
.All test methods must perform at least one assertion (within a call hierarchy of max. 3 steps).
----
MATCH
  (testType:Test:Type)-[:DECLARES]->(testMethod:Test:Method)
WHERE
  NOT (testMethod)-[:INVOKES*..3]->(:Method:Assert)
RETURN
  testType AS DeclaringType,
  testMethod AS Method
----

=== Reports

[[test:TestAffectedByLastCommit]]
[source,cypher,role=concept,requiresConcepts="junit4:TestClass,vcs:CurrentBranch",severity=INFO]
.Creates a report about all test classes that are affected by changes to Java types by the last commit on the current branch.
----
MATCH
  (:Branch:Current)-[:HAS_HEAD]->(:Commit)-[:CONTAINS_CHANGE]->()-[:MODIFIES]->(sourceFile:File)
WHERE
  sourceFile.relativePath ends with ".java"
WITH
  sourceFile
MATCH
  (artifact:Artifact)-[:CONTAINS]->(package:Package)-[:CONTAINS]->(type:Type)
WHERE
  sourceFile.relativePath ends with (package.fileName + "/" + type.sourceFileName)
WITH
  artifact, type
MATCH
  (test:Class)-[:EXTENDS|IMPLEMENTS*0..]->(:Test:Class)-[:DEPENDS_ON*0..1]->(abstractType:Type)<-[:EXTENDS|IMPLEMENTS*0..]-(type:Type),
  (:Artifact)-[:CONTAINS]->(abstractType)
WHERE NOT
  (has(test.abstract) and test.abstract)
RETURN
  artifact.name as Artifact, test.fqn as Test, type.fqn as ModifiedTypes
ORDER BY
  Artifact, size(ModifiedTypes) desc, Test asc
----
